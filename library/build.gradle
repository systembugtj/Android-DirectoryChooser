apply plugin: 'com.android.library'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'checkstyle'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    jcenter()
    maven {url "https://clojars.org/repo/"}
    maven { url 'http://guardian.github.com/maven/repo-releases' }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}


dependencies {
    compile 'com.android.support:palette-v7:23.3.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:design:23.3.0'

    compile 'com.android.support:support-annotations:23.3.0'
    compile 'com.gu:option:1.3'
    compile 'com.google.auto.value:auto-value:1.2'
    apt 'frankiesardo:auto-parcel:1.0.3'


    testCompile 'junit:junit:4.12'

    // Required -- JUnit 4 framework
    testCompile 'junit:junit:4.12'
    // Optional -- Mockito framework
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'com.squareup.assertj:assertj-android:1.1.0'
    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3' // fixes issue on linux/mac
    testCompile('org.robolectric:robolectric:3.0')
    testCompile('org.robolectric:shadows-support-v4:3.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}


checkstyle {
    configFile project.file('../config/checkstyle/checkstyle.xml')
    showViolations true
}

def libraryGroupId = 'com.conouch.view'
def libraryArtifactId = 'choose-directory'
def libraryVersion = "1.0.1"

publishing {
    publications {
        aar(MavenPublication) {
            groupId libraryGroupId
            version libraryVersion
            artifactId libraryArtifactId

            artifact("$buildDir/outputs/aar/${project.name}-release.aar")
        }
    }
}

artifactory {
    contextUrl = artifactory_context_url
    publish {
        repository {
            repoKey = 'libs-release-local'

            username = artifactory_username
            password = artifactory_password
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}